
// huffware script: bouncer, by fred huffhines.
//
// this script repels an object or avatar that slams into it.
//
//   this script is licensed by the GPL v3 which is documented at: http://www.gnu.org/licenses/gpl.html
//   do not use it in objects without fully realizing you are implicitly accepting that license.
//

integer PUSH_UPWARDS = TRUE;
    // if this is true, the velocity is reflected upwards.  this is sort of
    // a trampoline effect.
integer PUSH_DIRECTLY_BACK = TRUE;
    // if this is true, then the object's velocity is reflected directly back.
    // this is not a realistic bounce since it is like every bounce hits the
    // object at a tangent rather than having the angle be rotated as a
    // reflection.

integer AVATARS_ONLY = FALSE;
    // if true, then the bouncer will only react to people and not things.

float PUSH_MULTIPLIER = 32.0;
    // increase the object's velocity by this factor, as part of how we
    // help it along when it collides with us.

integer LOCAL_AXIS = FALSE;
    // use the axis relative to the object's rotation?

// originally came from reverse_velocity from concussive script.
vector push_back(key act_upon, integer local_axis)
{
    list details = llGetObjectDetails(act_upon, [ OBJECT_VELOCITY ]);
    vector current_velocity = llList2Vector(details, 0);    

    if (local_axis) {
        rotation rot = llGetRot();
        current_velocity /= rot;  // undo the rotation.
    }
    // here's where we return the velocity exactly backwards along
    // its own axis.  this is not really very true to physics.
    vector new_velocity = -PUSH_MULTIPLIER * current_velocity;
    new_velocity *= llGetMass();
//    llPushObject(act_upon, new_velocity, ZERO_VECTOR, local_axis);
    return new_velocity;
}

// causes the object to be thrown up into the air and magnifies their
// velocity.
vector push_up(key act_upon, integer local_axis)
{
    list details = llGetObjectDetails(act_upon, [ OBJECT_VELOCITY ]);
    vector current_velocity = llList2Vector(details, 0);
    if (local_axis) {
        rotation rot = llGetRot();
        current_velocity /= rot;  // undo the rotation.
    }
    // another example that's like the trampoline...
//    vector new_velocity = PUSH_MULTIPLIER * llRot2Up(llGetRot());
    vector new_velocity = current_velocity;
    new_velocity.z = -PUSH_MULTIPLIER * new_velocity.z;
    new_velocity *= llGetMass();
//    llPushObject(act_upon, new_velocity, ZERO_VECTOR, local_axis);
    return new_velocity;
}

// a newer method that can be used to 'continue' the velocity away from
// the bouncer but more upwards from an elastic collision than back at the av.
/////not implemented yet.
// needs good physics normal vector implementation.

// called by the collision event handler when we detect something
// bumping into us.
handle_collisions(integer count)
{
    integer i;
    for (i = 0; i < count; i++) {
        if ( (AVATARS_ONLY && (llDetectedType(i) & AGENT) )
                || !AVATARS_ONLY) {
            vector upwards;
            if (PUSH_UPWARDS)
                upwards = push_up(llDetectedKey(i), LOCAL_AXIS);
            vector back;
            if (PUSH_DIRECTLY_BACK)
                back = push_back(llDetectedKey(i), LOCAL_AXIS);
            llPushObject(llDetectedKey(i), upwards + back, ZERO_VECTOR, LOCAL_AXIS);
        }
    }
}

//////////////
// from hufflets...

//////////////
// huffware script: auto-retire, by fred huffhines, version 2.5.
// distributed under BSD-like license.
//   !!  keep in mind that this code must be *copied* into another
//   !!  script that you wish to add auto-retirement capability to.
// when a script has auto_retire in it, it can be dropped into an
// object and the most recent version of the script will destroy
// all older versions.
//
// the version numbers are embedded into the script names themselves.
// the notation for versions uses a letter 'v', followed by two numbers
// in the form "major.minor".
// major and minor versions are implicitly considered as a floating point
// number that increases with each newer version of the script.  thus,
// "hazmap v0.1" might be the first script in the "hazmap" script continuum,
// and "hazmap v3.2" is a more recent version.
//
// example usage of the auto-retirement script:
//     default {
//         state_entry() {
//            auto_retire();  // make sure newest addition is only version of script.
//        }
//     }
// this script is partly based on the self-upgrading scripts from markov brodsky
// and jippen faddoul.
//////////////
auto_retire() {
    string self = llGetScriptName();  // the name of this script.
    list split = compute_basename_and_version(self);
    if (llGetListLength(split) != 2) return;  // nothing to do for this script.
    string basename = llList2String(split, 0);  // script name with no version attached.
    string version_string = llList2String(split, 1);  // the version found.
    integer posn;
    // find any scripts that match the basename.  they are variants of this script.
    for (posn = llGetInventoryNumber(INVENTORY_SCRIPT) - 1; posn >= 0; posn--) {
//log_it("invpo=" + (string)posn);
        string curr_script = llGetInventoryName(INVENTORY_SCRIPT, posn);
        if ( (curr_script != self) && (llSubStringIndex(curr_script, basename) == 0) ) {
            // found a basic match at least.
            list inv_split = compute_basename_and_version(curr_script);
            if (llGetListLength(inv_split) == 2) {
                // see if this script is more ancient.
                string inv_version_string = llList2String(inv_split, 1);  // the version found.
                // must make sure that the retiring script is completely the identical basename;
                // just matching in the front doesn't make it a relative.
                if ( (llList2String(inv_split, 0) == basename)
                    && ((float)inv_version_string < (float)version_string) ) {
                    // remove script with same name from inventory that has inferior version.
                    llRemoveInventory(curr_script);
                }
            }
        }
    }
}
//
// separates the base script name and version number.  used by auto_retire.
list compute_basename_and_version(string to_chop_up)
{
    // minimum script name is 2 characters plus a version.
    integer space_v_posn;
    // find the last useful space and 'v' combo.
    for (space_v_posn = llStringLength(to_chop_up) - 3;
        (space_v_posn >= 2) && (llGetSubString(to_chop_up, space_v_posn, space_v_posn + 1) != " v");
        space_v_posn--) {
        // look for space and v but do nothing else.
//log_it("pos=" + (string)space_v_posn);
    }
    if (space_v_posn < 2) return [];  // no space found.
//log_it("space v@" + (string)space_v_posn);
    // now we zoom through the stuff after our beloved v character and find any evil
    // space characters, which are most likely from SL having found a duplicate item
    // name and not so helpfully renamed it for us.
    integer indy;
    for (indy = llStringLength(to_chop_up) - 1; indy > space_v_posn; indy--) {
//log_it("indy=" + (string)space_v_posn);
        if (llGetSubString(to_chop_up, indy, indy) == " ") {
            // found one; zap it.  since we're going backwards we don't need to
            // adjust the loop at all.
            to_chop_up = llDeleteSubString(to_chop_up, indy, indy);
//log_it("saw case of previously redundant item, aieee.  flattened: " + to_chop_up);
        }
    }
    string full_suffix = llGetSubString(to_chop_up, space_v_posn, -1);
    // ditch the space character for our numerical check.
    string chop_suffix = llGetSubString(full_suffix, 1, llStringLength(full_suffix) - 1);
    // strip out a 'v' if there is one.
    if (llGetSubString(chop_suffix, 0, 0) == "v")
        chop_suffix = llGetSubString(chop_suffix, 1, llStringLength(chop_suffix) - 1);
    // if valid floating point number and greater than zero, that works for our version.
    string basename = to_chop_up;  // script name with no version attached.
    if ((float)chop_suffix > 0.0) {
        // this is a big success right here.
        basename = llGetSubString(to_chop_up, 0, -llStringLength(full_suffix) - 1);
        return [ basename, chop_suffix ];
    }
    // seems like we found nothing useful.
    return [];
}
//
//////////////

// end hufflets.
//////////////


default
{
    state_entry() { if (llSubStringIndex(llGetObjectName(),  "huffotronic") < 0) state real_default; }
    on_rez(integer parm) { state rerun; }
}
state rerun { state_entry() { state default; } }

state real_default
{
    state_entry() { auto_retire(); }

    collision(integer count) { handle_collisions(count); }
}

